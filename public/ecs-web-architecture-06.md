---
title: ã€AWSã€‘ECSã‚’ç”¨ã„ãŸç°¡æ˜“webæ§‹ç¯‰â‘¥(DynamoDBãƒ»S3ãƒ»IAM)
tags:
  - 'AWS'
  - 'DynamoDB'
  - 'S3'
  - 'IAM'
private: true
updated_at: ''
id: null
organization_url_name: null
slide: false
ignorePublish: false
---

# ç¬¬6ç«  ãƒ‡ãƒ¼ã‚¿ã‚¹ãƒˆã‚¢è¨­è¨ˆãƒ»IAMè¨­è¨ˆï¼ˆDynamoDB/S3/IAMï¼‰

æœ¬ç« ã¯DynamoDB/S3/IAMã®æ§‹ç¯‰ã§ã™ã€‚
ã“ã®ç« ã®å†…å®¹ã§æœ¬ã‚·ãƒªãƒ¼ã‚ºã§ä½œæˆã—ã¦ã„ãŸwebãƒšãƒ¼ã‚¸ãŒå®Œæˆã—ã¾ã™ã€‚

![å®Œæˆã‚¤ãƒ¡ãƒ¼ã‚¸](../images/ECS-web-architecture/webãƒšãƒ¼ã‚¸.jpg)

## ðŸ“š ç›®æ¬¡

* [1. æ§‹æˆã®èª¬æ˜Ž](#1-æ§‹æˆã®èª¬æ˜Ž)
* [2. ä½œæˆæ–¹æ³•](#2-ä½œæˆæ–¹æ³•)
* [3. Terraformæ§‹æˆ](#3-terraformæ§‹æˆ)
* [4. æŒ¯ã‚Šè¿”ã‚Š](#4-æŒ¯ã‚Šè¿”ã‚Š)
* [5. å‚ç…§](#5-å‚ç…§)
* [6. ã‚³ãƒ¼ãƒ‰å…¨ä½“](#6-ã‚³ãƒ¼ãƒ‰å…¨ä½“)

## 1. æ§‹æˆã®èª¬æ˜Ž

### ä½•ã‚’ã—ãŸã„ã‹

ECSï¼ˆFargateï¼‰ä¸Šã§å‹•ä½œã™ã‚‹ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã‹ã‚‰ã€

* **æ§‹é€ åŒ–ãƒ‡ãƒ¼ã‚¿** ã‚’ DynamoDB ã«ä¿å­˜ãƒ»å‚ç…§
* **ãƒ•ã‚¡ã‚¤ãƒ«ãƒ‡ãƒ¼ã‚¿** ã‚’ S3 ã«ä¿å­˜ãƒ»å–å¾—

---

### ã©ã‚“ãªæ§‹æˆã‹ãƒ»ä½•ãŒã§ãã‚‹ã‹

* ECS ã‚¿ã‚¹ã‚¯ã¯ **IAM ãƒ­ãƒ¼ãƒ«ï¼ˆã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ï¼‰** ã‚’é€šã˜ã¦ DynamoDB / S3 ã«ã‚¢ã‚¯ã‚»ã‚¹
* èªè¨¼æƒ…å ±ï¼ˆã‚¢ã‚¯ã‚»ã‚¹ã‚­ãƒ¼ç­‰ï¼‰ã‚’ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã«æŒãŸã›ãªã„

![æ§‹æˆå›³](../images/ECS-web-architecture/ECS-DynamoDB-S3-CICD-arc.jpg)

æ§‹æˆä¸Šã®ãƒã‚¤ãƒ³ãƒˆã¯ä»¥ä¸‹ã®é€šã‚Šã€‚

* **IAM ãƒ­ãƒ¼ãƒ«ãƒ™ãƒ¼ã‚¹èªå¯** ã«ã‚ˆã‚‹ã‚»ã‚­ãƒ¥ã‚¢ãªã‚¢ã‚¯ã‚»ã‚¹åˆ¶å¾¡
* æ°¸ç¶šãƒ‡ãƒ¼ã‚¿ã¯ã™ã¹ã¦ãƒžãƒãƒ¼ã‚¸ãƒ‰ã‚µãƒ¼ãƒ“ã‚¹ã§ç®¡ç†

---

## 2. ä½œæˆæ–¹æ³•

### å‰ææ¡ä»¶

ä»¥ä¸‹ãŒãƒ­ãƒ¼ã‚«ãƒ«ç’°å¢ƒã«ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã•ã‚Œã¦ã„ã‚‹ã“ã¨ã‚’å‰æã¨ã™ã‚‹ã€‚

* Terraform

---

### æ§‹ç¯‰é †åº

1. DynamoDB ãƒ†ãƒ¼ãƒ–ãƒ«ã®ä½œæˆ
2. S3 ãƒã‚±ãƒƒãƒˆã®ä½œæˆ
3. DynamoDB / S3 ã¸ã®ã‚¢ã‚¯ã‚»ã‚¹ç”¨ IAM ãƒãƒªã‚·ãƒ¼ã®ä½œæˆ
4. ECS ã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ã®ä½œæˆ
5. ã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ã¸ã®ãƒãƒªã‚·ãƒ¼ã‚¢ã‚¿ãƒƒãƒ
6. S3ã«ç”»åƒãƒ•ã‚¡ã‚¤ãƒ«ã‚’ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰, DynamoDBã«ãƒ‡ãƒ¼ã‚¿ä¿å­˜

---
6.ã«ã¤ã„ã¦è£œè¶³
S3ã«ä¸‹è¨˜æ§‹æˆã§ãƒ•ã‚©ãƒ«ãƒ€ã‚’ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰
â€»ç”»åƒã¯ã”ç”¨æ„ãã ã•ã„ã€‚
```text
images/
    â”œâ”€â”€ apple.jpg
    â”œâ”€â”€ banana.jpg
    â””â”€â”€ orange.jpg
```
DynamoDBã«ä¸‹è¨˜ãƒ‡ãƒ¼ã‚¿ã‚’è¿½åŠ 
```json
{
  "id": {
    "S": "1"
  },
  "price": {
    "S": "100"
  },
  "title": {
    "S": "ãƒãƒŠãƒŠ"
  },
      "description": {
    "S": "ãƒãƒŠãƒŠã€‚"
  },
  "image_key": {
    "S": "images/banana.jpg"
  }
}
```

```json
{
  "id": {
    "S": "2"
  },
  "title": {
    "S": "ãƒªãƒ³ã‚´"
  },
  "price": {
    "N": "150"
  },
  "description": {
    "S": "ãƒªãƒ³ã‚´ã€‚"
  },
  "image_key": {
    "S": "images/apple.jpg"
  }
}
```
```json
{
  "id": {
    "S": "3"
  },
  "title": {
    "S": "ã‚ªãƒ¬ãƒ³ã‚¸"
  },
  "price": {
    "N": "200"
  },
  "description": {
    "S": "ã‚ªãƒ¬ãƒ³ã‚¸ã€‚"
  },
  "image_key": {
    "S": "images/orange.jpg"
  }
}
```

---

### ä¸»è¦ãªè¨­å®šå†…å®¹ã¨æ³¨æ„ç‚¹

#### DynamoDB

* èª²é‡‘ãƒ¢ãƒ¼ãƒ‰ï¼šã‚ªãƒ³ãƒ‡ãƒžãƒ³ãƒ‰

---

#### S3

* ãƒãƒ¼ã‚¸ãƒ§ãƒ‹ãƒ³ã‚°ï¼šç„¡åŠ¹ï¼ˆå­¦ç¿’ç”¨é€”ã®ãŸã‚ï¼‰
* ã‚µãƒ¼ãƒãƒ¼ã‚µã‚¤ãƒ‰æš—å·åŒ–ï¼šæœ‰åŠ¹ï¼ˆAES256ï¼‰
* `force_destroy = true` ã‚’è¨­å®šï¼ˆå­¦ç¿’ç”¨é€”ã®ãŸã‚ï¼‰

â€»**æœ¬ç•ªç”¨é€”ã§ã¯ `force_destroy` ã¯éžæŽ¨å¥¨**

---

#### IAM
| åˆ†é¡ž | ãƒªã‚½ãƒ¼ã‚¹å | è¨±å¯ã‚¢ã‚¯ã‚·ãƒ§ãƒ³ | æ¨©é™ã®å¯¾è±¡ï¼ˆResourceï¼‰ | å½¹å‰²ãƒ»ç”¨é€” |
| --- | --- | --- | --- | --- |
| **ãƒãƒªã‚·ãƒ¼** | `s3_access_policy` | ListBucket, GetObject, PutObject, DeleteObject | S3ãƒã‚±ãƒƒãƒˆæœ¬ä½“ãŠã‚ˆã³é…ä¸‹ã®ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆ | S3ã®èª­ã¿æ›¸ããƒ»ä¸€è¦§è¡¨ç¤º |
| **ãƒãƒªã‚·ãƒ¼** | `dynamodb_access_policy` | Scan, GetItem, Query, DescribeTable | æŒ‡å®šã—ãŸDynamoDBãƒ†ãƒ¼ãƒ–ãƒ« | DynamoDBã®ãƒ‡ãƒ¼ã‚¿èª­ã¿å–ã‚Š |
| **ãƒ­ãƒ¼ãƒ«** | `ecs_task_execution_role` | ECRã‹ã‚‰ã®ã‚¤ãƒ¡ãƒ¼ã‚¸å–å¾—ã€CloudWatch Logsã¸ã®å‡ºåŠ›ãªã© | AWSç®¡ç†ãƒãƒªã‚·ãƒ¼ã«ã‚ˆã‚Šå®šç¾© | **ECSè‡ªä½“**ãŒã‚³ãƒ³ãƒ†ãƒŠã‚’èµ·å‹•ã™ã‚‹ãŸã‚ã«ä½¿ã†æ¨©é™ |
| **ãƒ­ãƒ¼ãƒ«** | `ecs_task_role` | ä¸Šè¨˜2ã¤ã®S3/DynamoDBæ“ä½œ | S3ãƒã‚±ãƒƒãƒˆãƒ»DynamoDBãƒ†ãƒ¼ãƒ–ãƒ« | **ã‚³ãƒ³ãƒ†ãƒŠå†…ã®ã‚¢ãƒ—ãƒª**ãŒAWSãƒªã‚½ãƒ¼ã‚¹ã‚’æ“ä½œã™ã‚‹ãŸã‚ã®æ¨©é™ |


* ECS ã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ã‚’ä½¿ç”¨
* æœ€å°æ¨©é™ã®åŽŸå‰‡ã«å¾“ã„ã€æ“ä½œã‚’é™å®š

---

## 3. Terraformæ§‹æˆ

### ãƒ•ã‚©ãƒ«ãƒ€æ§‹æˆï¼ˆä¾‹ï¼‰

```
terraform/
â”œâ”€â”€ modules/
â”‚   â”œâ”€â”€ dynamodb/
â”‚   â”œâ”€â”€ s3/
â”‚   â””â”€â”€ iam/
â”œâ”€â”€ backend.tf
â”œâ”€â”€ provider.tf
â”œâ”€â”€ main.tf
â”œâ”€â”€ variables.tf
â””â”€â”€ outputs.tf
```
[6.ã‚³ãƒ¼ãƒ‰å…¨ä½“](#6-ã‚³ãƒ¼ãƒ‰å…¨ä½“)ã«terraformé…ä¸‹ã®ã‚³ãƒ¼ãƒ‰ã‚‚æŽ²è¼‰ã—ã¦ã„ã¾ã™ã€‚  
å®Ÿéš›ã«æ§‹ç¯‰ã•ã‚Œã‚‹æ–¹ã¯å‚è€ƒã«ã—ã¦ãã ã•ã„ã€‚
â€»terraformé…ä¸‹ã®ã‚³ãƒ¼ãƒ‰ã«ã¤ã„ã¦ã®èª¬æ˜Žã¯çœç•¥ã—ã¾ã™ã€‚

---

### ã‚³ãƒ¼ãƒ‰ã®è§£èª¬

#### DynamoDB

**aws_dynamodb_table**
```hcl
resource "aws_dynamodb_table" "app_table" {
  name           = var.table_name
  billing_mode   = var.billing_mode # ã‚ªãƒ³ãƒ‡ãƒžãƒ³ãƒ‰ãƒ¢ãƒ¼ãƒ‰ã‚’æŒ‡å®š
  hash_key       = var.hash_key # ãƒ‘ãƒ¼ãƒ†ã‚£ã‚·ãƒ§ãƒ³ã‚­ãƒ¼
  range_key      = var.range_key # ã‚½ãƒ¼ãƒˆã‚­ãƒ¼
  # --- ç•¥ ---
}
```
* å¸¸æ™‚ã‚¢ã‚¯ã‚»ã‚¹ãŒã‚ã‚‹ã‚ã‘ã§ã¯ãªã„ã®ã§ã‚ªãƒ³ãƒ‡ãƒžãƒ³ãƒ‰ãƒ¢ãƒ¼ãƒ‰ã‚’é¸æŠžã—ã€ã‚³ã‚¹ãƒˆæœ€é©åŒ–

**ãƒ‘ãƒ¼ãƒ†ã‚£ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ (Hash Key)**
å½¹å‰²: ãƒ‡ãƒ¼ã‚¿ã‚’ã©ã®ç‰©ç†çš„ãªã‚µãƒ¼ãƒãƒ¼ï¼ˆãƒ‘ãƒ¼ãƒ†ã‚£ã‚·ãƒ§ãƒ³ï¼‰ã«ä¿å­˜ã™ã‚‹ã‹ã‚’æ±ºã‚ã‚‹ãŸã‚ã®éµã€‚

**ã‚½ãƒ¼ãƒˆã‚­ãƒ¼ (Range Key)**
å½¹å‰²: åŒã˜ãƒ‘ãƒ¼ãƒ†ã‚£ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ã®ä¸­ã§ã€ãƒ‡ãƒ¼ã‚¿ã‚’ä¸¦ã¹æ›¿ãˆã‚‹ãŸã‚ã®éµã€‚

---

**dynamic "attribute"**
```hcl
  dynamic "attribute" {
    for_each = var.attributes
    content {
      name = attribute.value.name
      type = attribute.value.type
    }
  }
```
å¤‰æ•°ï¼ˆãƒªã‚¹ãƒˆã‚„ãƒžãƒƒãƒ—ï¼‰ã®ä¸­èº«ã«åˆã‚ã›ã¦ã€attribute ãƒ–ãƒ­ãƒƒã‚¯ã‚’è‡ªå‹•çš„ã«é‡ç”£ã™ã‚‹ã€‚
ãƒ†ãƒ¼ãƒ–ãƒ«ã«ã‚ˆã£ã¦ã‚­ãƒ¼ã®æ•°ã‚„åå‰ãŒå¤‰ã‚ã‚‹ãŸã‚ã€dynamic ãƒ–ãƒ­ãƒƒã‚¯ã‚’ä½¿ç”¨ã—ã¦æ±Žç”¨æ€§ã‚’æŒãŸã›ã¦ã„ã‚‹ã€‚  
å…·ä½“çš„ãªå€¤ã¯ varialbles.tfã§ãƒªã‚¹ãƒˆå½¢å¼ã§æ¸¡ã—ã¦ã„ã‚‹ã€‚

---

**dynamic "global_secondary_index"**
```hcl
  dynamic "global_secondary_index" {
    for_each = var.global_secondary_indexes
    content {
      name            = global_secondary_index.value.name
      hash_key        = global_secondary_index.value.hash_key
      range_key       = global_secondary_index.value.range_key
      projection_type = global_secondary_index.value.projection_type
    }
  }
```
æœ¬æ§‹æˆã§ã¯ä½¿ç”¨ã—ãªã„ãŒã€å°†æ¥çš„ã«åˆ¥ã®ãƒ†ãƒ¼ãƒ–ãƒ«ã§GSIã‚’ä½¿ãˆã‚‹ã‚ˆã†ã«è¨˜è¿°ã€‚

**GSI**
æœ¬æ¥ã®ãƒ‘ãƒ¼ãƒ†ã‚£ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ï¼ˆãƒ¦ãƒ¼ã‚¶ãƒ¼IDãªã©ï¼‰ä»¥å¤–ã®é …ç›®ã‚’ä½¿ã£ã¦ã€ç‰¹å®šã®é …ç›®ã‚’ã‚­ãƒ¼ã¨ã—ã¦æ©Ÿèƒ½ã•ã›ã¦é«˜é€Ÿã«æ¤œç´¢ã™ã‚‹ãŸã‚ã®ä»•çµ„ã¿ã€‚


---

 **point_in_time_recovery**

```hcl
  point_in_time_recovery {
    enabled = var.point_in_time_recovery
  }
```
éŽåŽ»35æ—¥é–“ã®ä»»æ„ã®æ™‚ç‚¹ã«ãƒ‡ãƒ¼ã‚¿ã‚’å¾©å…ƒã§ãã‚‹**æ©Ÿèƒ½ã®æœ‰åŠ¹åŒ–è¨­å®š

---

**server_side_encryption**
```hcl
  server_side_encryption {
    enabled = var.server_side_encryption
  }
```
 ã‚µãƒ¼ãƒãƒ¼å´ã®æš—å·åŒ–ã‚’è¨­å®š
 
---
#### S3
```hcl
resource "aws_s3_bucket" "storage_bucket" {
  bucket = var.storage_bucket_name
  force_destroy = true
}
```
å­¦ç¿’ç”¨é€”ã§é »ç¹ã«ä½œæˆãƒ»å‰Šé™¤ã‚’è¡Œã†ãŸã‚ã€force_destroyã‚’æœ‰åŠ¹åŒ–
â€»å®Ÿé‹ç”¨ã§ã¯éžæŽ¨å¥¨

```hcl
resource "aws_s3_bucket_server_side_encryption_configuration" "storage_bucket_sse" {
  bucket = aws_s3_bucket.storage_bucket.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}
```
æš—å·åŒ–è¨­å®šã‚’æ˜Žç¤ºçš„ã«å®šç¾©

---

#### IAM

**S3ã‚¢ã‚¯ã‚»ã‚¹ç”¨ãƒãƒªã‚·ãƒ¼**
```hcl
resource "aws_iam_policy" "s3_access_policy" {
  name        = var.s3_access_policy_name
  description = "IAM policy for S3 access"
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect   = "Allow",
        Action   = ["s3:ListBucket"],
        Resource = var.storage_s3_arn
      },
      {
        Effect   = "Allow",
        Action   = ["s3:GetObject", "s3:PutObject", "s3:DeleteObject"],
        Resource = "${var.storage_s3_arn}/*"
      }
    ]
  })
}
```
S3ã®IAMãƒãƒªã‚·ãƒ¼ã§ã€ãƒã‚±ãƒƒãƒˆåã®ã¿ã®æŒ‡å®šï¼ˆarn:aws:s3:::bucket-nameï¼‰ã¨ã€æœ«å°¾ã«ã‚¹ãƒ©ãƒƒã‚·ãƒ¥ã¨ãƒ¯ã‚¤ãƒ«ãƒ‰ã‚«ãƒ¼ãƒ‰ã‚’ä»˜ã‘ãŸæŒ‡å®šï¼ˆarn:aws:s3:::bucket-name/*ï¼‰ã‚’ä½¿ã„åˆ†ã‘ã¦ã„ã‚‹ç†ç”±ã¯ã€ã€Œæ“ä½œã®å¯¾è±¡ãŒã€ç®±ï¼ˆãƒã‚±ãƒƒãƒˆï¼‰ãªã®ã‹ã€ä¸­èº«ï¼ˆã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆï¼‰ãªã®ã‹ã€ã¨ã„ã†é•ã„ãŒã‚ã‚‹ã‹ã‚‰ã§ã™ã€‚

| ã‚¢ã‚¯ã‚·ãƒ§ãƒ³ | å¯¾è±¡ (Resource) | æ¨©é™ã®ç¨®é¡ž | å†…å®¹ |
| --- | --- | --- | --- |
| `s3:ListBucket` | ãƒã‚±ãƒƒãƒˆè‡ªä½“ | **ãƒã‚±ãƒƒãƒˆå˜ä½** | ãƒã‚±ãƒƒãƒˆå†…ã®ãƒ•ã‚¡ã‚¤ãƒ«ä¸€è¦§ï¼ˆãƒªã‚¹ãƒˆï¼‰ã‚’å–å¾—ã™ã‚‹ã€‚ |
| `s3:GetObject` | ãƒã‚±ãƒƒãƒˆã®ä¸­èº« (`/*`) | **ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆå˜ä½** | ãƒ•ã‚¡ã‚¤ãƒ«ã‚’èª­ã¿å–ã‚‹ï¼ˆãƒ€ã‚¦ãƒ³ãƒ­ãƒ¼ãƒ‰ã™ã‚‹ï¼‰ã€‚ |
| `s3:PutObject` | ãƒã‚±ãƒƒãƒˆã®ä¸­èº« (`/*`) | **ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆå˜ä½** | ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä¿å­˜ãƒ»æ›´æ–°ï¼ˆã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ï¼‰ã™ã‚‹ã€‚ |
| `s3:DeleteObject` | ãƒã‚±ãƒƒãƒˆã®ä¸­èº« (`/*`) | **ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆå˜ä½** | ãƒ•ã‚¡ã‚¤ãƒ«ã‚’å‰Šé™¤ã™ã‚‹ã€‚ |

---

**DynamoDBã‚¢ã‚¯ã‚»ã‚¹ç”¨ãƒãƒªã‚·ãƒ¼**
```hcl
resource "aws_iam_policy" "dynamodb_access_policy" {
  name        = var.dynamodb_access_policy_name
  description = "IAM policy for DynamoDB access"
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect = "Allow",
        Action = [
          "dynamodb:Scan",
          "dynamodb:GetItem",
          "dynamodb:Query",
          "dynamodb:DescribeTable"
        ],
        Resource = var.dynamodb_table_arn
      }
    ]
  })
}
```
| ã‚¢ã‚¯ã‚·ãƒ§ãƒ³ | æ„å‘³ | å†…å®¹ |
| --- | --- | --- |
| `dynamodb:GetItem` | **1ä»¶å–å¾—** | ãƒ‘ãƒ¼ãƒ†ã‚£ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ã‚’æŒ‡å®šã—ã¦ã€ç‰¹å®šã®1ä»¶ã®ãƒ‡ãƒ¼ã‚¿ã‚’ç›´æŽ¥å–å¾—ã€‚ |
| `dynamodb:Query` | **æ¡ä»¶æ¤œç´¢** | ã‚­ãƒ¼ã‚’ä½¿ã£ã¦ã€ç‰¹å®šã®ç¯„å›²ã®ãƒ‡ãƒ¼ã‚¿ã‚’åŠ¹çŽ‡çš„ã«æ¤œç´¢ã€‚ |
| `dynamodb:Scan` | **å…¨ä»¶å–å¾—** | ãƒ†ãƒ¼ãƒ–ãƒ«ã«ã‚ã‚‹ã™ã¹ã¦ã®ãƒ‡ãƒ¼ã‚¿ã‚’ç«¯ã‹ã‚‰é †ã«èª­ã¿å‡ºã™ã€‚ |
| `dynamodb:DescribeTable` | **è¨­å®šç¢ºèª** | ãƒ†ãƒ¼ãƒ–ãƒ«ã®ã‚¹ãƒ†ãƒ¼ã‚¿ã‚¹ï¼ˆèµ·å‹•ä¸­ã‹ãªã©ï¼‰ã‚„é …ç›®æ•°ã€ã‚µã‚¤ã‚ºãªã©ã®æƒ…å ±ã‚’å–å¾—ã€‚ |

---

**ECSã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«**
```hcl
resource "aws_iam_role" "ecs_task_role" {
  name               = var.ecs_task_role_name
  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Action    = "sts:AssumeRole",
        Effect    = "Allow",
        Principal = {
          Service = "ecs-tasks.amazonaws.com"
        }
      }
    ]
  })
}
```

```hcl
resource "aws_iam_role_policy_attachment" "ecs_task_role_dynamodb_access" {
  role       = aws_iam_role.ecs_task_role.name
  policy_arn = aws_iam_policy.dynamodb_access_policy.arn
}

resource "aws_iam_role_policy_attachment" "ecs_task_role_s3_access" {
  role       = aws_iam_role.ecs_task_role.name
  policy_arn = aws_iam_policy.s3_access_policy.arn
}
```
ECSã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ã‚’ä½œæˆã—ã€S3,DynamoDBã‚¢ã‚¯ã‚»ã‚¹ç”¨ãƒãƒªã‚·ãƒ¼ã‚’ã‚¢ã‚¿ãƒƒãƒã€‚

---

**ECSã‚¿ã‚¹ã‚¯å®Ÿè¡Œãƒ­ãƒ¼ãƒ«**
```hcl
resource "aws_iam_role" "ecs_task_execution_role" {
  name               = var.ecs_task_execution_role_name
  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Action    = "sts:AssumeRole",
        Effect    = "Allow",
        Principal = {
          Service = "ecs-tasks.amazonaws.com"
        }
      }
    ]
  })
}
```

```hcl
resource "aws_iam_role_policy_attachment" "ecs_task_execution_role_policy" {
  role       = aws_iam_role.ecs_task_execution_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}
```
ECSã‚¿ã‚¹ã‚¯å®Ÿè¡Œãƒ­ãƒ¼ãƒ«ã‚’ä½œæˆã—ã€AWSç®¡ç†ã®ECSå®Ÿè¡Œãƒ­ãƒ¼ãƒ«ãƒãƒªã‚·ãƒ¼ã‚’ã‚¢ã‚¿ãƒƒãƒã€‚

---

## 4. æŒ¯ã‚Šè¿”ã‚Š

### æ”¹å–„ã§ãã‚‹ç‚¹

* DynamoDB ã®ã‚¢ã‚¯ã‚»ã‚¹æ¨©é™ãŒã‚„ã‚„åºƒã„
* S3 ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ‹ãƒ³ã‚°ãƒ»ãƒ©ã‚¤ãƒ•ã‚µã‚¤ã‚¯ãƒ«æœªè¨­å®š

---

### æ¬¡ã«ã‚„ã‚ŠãŸã„ã“ã¨

æ¬¡ç« ã§ã¯ã€
**CI/CDãƒ‘ã‚¤ãƒ—ãƒ©ã‚¤ãƒ³ã‚’å®Ÿè£…ã—ã€Github ActionsçµŒç”±ã§è‡ªå‹•ãƒ†ã‚¹ãƒˆãƒ»è‡ªå‹•ãƒ‡ãƒ—ãƒ­ã‚¤** å¯èƒ½ã«ã—ã¾ã™ã€‚

* **[â‘ æ§‹æˆã®èª¬æ˜Ž]()**  
* **[â‘¡S3(é™çš„Webã‚µã‚¤ãƒˆãƒ›ã‚¹ãƒ†ã‚£ãƒ³ã‚°)/ãƒ•ãƒ­ãƒ³ãƒˆã‚¨ãƒ³ãƒ‰]()**  
* **[â‘¢VPCé–¢é€£]()**  
* **[â‘£Docker/ãƒãƒƒã‚¯ã‚¨ãƒ³ãƒ‰]()**  
* **[â‘¤ECS + ALB]()**  
* **[â‘¥DynamoDBãƒ»S3é€£æº]()**  
* **[â‘¦CI/CD(ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³)]()** 
* **[â‘§CI/CD(ã‚¤ãƒ³ãƒ•ãƒ©)]()**

---

## 5. å‚ç…§

* [DynamoDBå…¬å¼ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆ](https://docs.aws.amazon.com/ja_jp/dynamodb/?icmpid=docs_homepage_featuredsvcs)
* [S3å…¬å¼ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆ](https://docs.aws.amazon.com/ja_jp/s3/?icmpid=docs_homepage_featuredsvcs)
* [IAMå…¬å¼ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆ](https://docs.aws.amazon.com/ja_jp/iam/)

---

## 6. ã‚³ãƒ¼ãƒ‰å…¨ä½“

* [terraform/provider.tf](#terraformprovidertf)
* [terraform/backend.tf](#terraformbackendtf)
* [terraform/variables.tf](#terraformvariablestf)
* [terraform/main.tf](#terraformmaintf)
* [terraform/modules/dynamodb/variables.tf](#terraformmodulesdynamodbvariablestf)
* [terraform/modules/dynamodb/main.tf](#terraformmodulesdynamodbmaintf)
* [terraform/modules/s3/variables.tf](#terraformmodules3variablestf)
* [terraform/modules/s3/main.tf](#terraformmoduless3maintf)
* [terraform/modules/iam/variables.tf](#terraformmodulesiamvariablestf)
* [terraform/modules/iam/main.tf](#terraformmodulesiammaintf)


### terraform/provider.tf
```hcl
provider "aws" {
  region  = var.aws_region
}

terraform {
  required_version = ">= 1.10.0, < 2.0.0"
  }
```
### terraform/backend.tf
```hcl
terraform {
  backend "s3" {
    key            = "dev/terraform.tfstate"
    region         = "ap-northeast-1"
    encrypt        = true
  }
}
```

### terraform/variables.tf
```hcl
variable "aws_region" {
  description = "AWS region"
  type        = string
  default     = "ap-northeast-1"
}

variable "aws_account_id" {
  description = "AWS Account ID"
  type        = string
  default     = "123456789012" # ã‚µãƒ³ãƒ—ãƒ«å€¤ã«å¤‰æ›´
}

variable "environment" {
  description = "Environment (e.g. dev, stg, prod)"
  type        = string
  default     = "dev"
}

variable "vpc_cidr" {
  description = "CIDR block for VPC"
  type        = string
  default     = "10.0.0.0/16"
}

variable "public_subnets" {
  description = "Public subnets map"
  type = map(object({
    cidr = string
    az   = string
  }))
  default = {
    "public-subnet-1a" = { cidr = "10.0.1.0/24", az = "ap-northeast-1a" },
    "public-subnet-1c" = { cidr = "10.0.2.0/24", az = "ap-northeast-1c" },
  }
}

variable "private_subnets" {
  description = "Private subnets map"
  type = map(object({
    cidr = string
    az   = string
  }))
  default = {
    "private-subnet-1a" = { cidr = "10.0.3.0/24", az = "ap-northeast-1a" },
    "private-subnet-1c" = { cidr = "10.0.4.0/24", az = "ap-northeast-1c" },
  }
}



variable "alb_ingress_source_ip" {
  description = "Allowed source IP for ALB ingress"
  type        = string
  default     = "0.0.0.0/0" # ã‚µãƒ³ãƒ—ãƒ«ã¨ã—ã¦åºƒç¯„å›²ãªIPã«å¤‰æ›´
}

variable "s3_web_bucket_access_ip" {
  description = "IP CIDR allowed to access web S3 bucket"
  type        = string
  default     = "0.0.0.0/0" # ã‚µãƒ³ãƒ—ãƒ«ã¨ã—ã¦åºƒç¯„å›²ãªIPã«å¤‰æ›´
}

variable "web_bucket_name" {
  description = "Web S3 bucket name"
  type        = string
  default     = "your-web-s3-bucket-name-example" # ã‚µãƒ³ãƒ—ãƒ«å€¤ã«å¤‰æ›´
}

variable "storage_bucket_name" {
  description = "Storage S3 bucket name"
  type        = string
  default     = "your-storage-s3-bucket-name-example" # ã‚µãƒ³ãƒ—ãƒ«å€¤ã«å¤‰æ›´
}

variable "api_ecr_image_tag" {
    description = "ECR repository image_tag"
  type        = string
  default     = "latest" # ã‚µãƒ³ãƒ—ãƒ«å€¤ã«å¤‰æ›´
}
```


### terraform/main.tf
```hcl
module "vpc" {
  source = "./modules/vpc"

  region                   = var.aws_region
  vpc_name                 = "${var.environment}-net-vpc-01"
  vpc_cidr                 = var.vpc_cidr
  enable_dns_support       = true
  enable_dns_hostnames     = true
  igw_name                 = "${var.environment}-net-igw-01"
  s3_gateway_endpoint_name = "${var.environment}-net-s3-gateway-01"
  public_route_table_name  = "${var.environment}-net-public-route-table-01"
  private_route_table_name = "${var.environment}-net-private-route-table-01"
  environment              = var.environment
  public_subnets           = var.public_subnets
  private_subnets          = var.private_subnets
}

module "vpc_endpoint" {
  source = "./modules/vpc_endpoint"
  
  region                   = var.aws_region
  vpce_sg_id               = module.sg.vpce_sg_id
  vpc_id                   = module.vpc.vpc_id  
  private_subnet_ids       = module.vpc.private_subnet_ids  
}

module "sg" {
  source = "./modules/sg"

  vpc_id           = module.vpc.vpc_id
  alb_sg_name      = "${var.environment}-net-sg-alb-01"
  alb_ingress_cidr = var.alb_ingress_source_ip
  ecs_sg_name      = "${var.environment}-net-sg-ecs-01"
  environment      = var.environment
  vpc_cidr         = var.vpc_cidr 
}

module "s3" {
  source = "./modules/s3"

  web_bucket_name           = var.web_bucket_name
  storage_bucket_name       = var.storage_bucket_name
  web_bucket_access_ip_cidr = var.s3_web_bucket_access_ip
  environment               = var.environment
}

module "iam" {
  source                       = "./modules/iam"
  aws_region                   = var.aws_region
  aws_account_id               = var.aws_account_id
  s3_access_policy_name        = "${var.environment}-sec-iam-policy-s3-access"
  dynamodb_access_policy_name  = "${var.environment}-sec-iam-policy-dynamodb-access"
  web_s3_arn                   = module.s3.web_bucket_arn
  storage_s3_arn               = module.s3.storage_bucket_arn
  dynamodb_table_arn           = module.dynamodb.table_arn
  ecs_task_execution_role_name = "${var.environment}-sec-iam-role-ecs-task-exe-01"
  ecs_task_role_name           = "${var.environment}-sec-iam-role-ecs-task-01"
  environment                  = var.environment
}

module "dynamodb" {
  source = "./modules/dynamodb"

  table_name      = "${var.environment}-app-data-table"
  hash_key        = "id"
  billing_mode    = "PAY_PER_REQUEST"
  attributes = [
    {
      name = "id"
      type = "S"
    }
  ]
  point_in_time_recovery = true
  server_side_encryption = true
  environment = var.environment
}

module "alb" {
  source = "./modules/alb"

  vpc_id                = module.vpc.vpc_id
  public_subnet_ids     = module.vpc.public_subnet_ids
  alb_name              = "${var.environment}-api-alb-01"
  alb_security_group_id = module.sg.alb_sg_id
  listener_port         = 80
  listener_protocol     = "HTTP"
  target_group_name     = "${var.environment}-api-alb-tg-01"
  target_group_port     = 3000
  target_group_protocol = "HTTP"
  health_check_path     = "/"
  environment           = var.environment
}

module "ecs" {
  source = "./modules/ecs"

  region                       = var.aws_region
  ecs_cluster_name             = "${var.environment}-api-ecs-cluster-01"
  task_definition_family       = "${var.environment}-api-ecs-task-01"
  task_cpu                     = 512
  task_memory                  = 1024
  ecs_task_execution_role_arn  = module.iam.ecs_task_execution_role_arn
  ecs_task_role_arn            = module.iam.ecs_task_role_arn
  container_name               = "${var.environment}-api-ecs-task-container-01"
  ecr_repository_url           = module.ecr.ecr_repository_url
  container_image_tag          = var.api_ecr_image_tag
  container_port               = 3000
  ecs_service_name             = "${var.environment}-api-ecs-service-01"
  desired_task_count           = 1
  private_subnet_ids           = module.vpc.private_subnet_ids
  ecs_security_group_id        = module.sg.ecs_sg_id
  alb_target_group_arn         = module.alb.target_group_arn
  dynamodb_table_name          = module.dynamodb.table_name
  s3_storage_bucket_name       = module.s3.storage_bucket_name
  environment                  = var.environment
  ecr_repository_name          = module.ecr.api_ecr_repository_name
}

module "ecr" {
  source = "./modules/ecr"

  ecr_name = "${var.environment}-api-ecr-01"
}
```

### terraform/modules/dynamodb/variables.tf
```hcl
variable "table_name" {
  description = "Name of the DynamoDB table"
  type        = string
}

variable "billing_mode" {
  description = "Controls how you are charged for read and write throughput"
  type        = string
  default     = "PAY_PER_REQUEST"
}

variable "hash_key" {
  description = "The attribute to use as the hash (partition) key"
  type        = string
}

variable "range_key" {
  description = "The attribute to use as the range (sort) key"
  type        = string
  default     = null
}

variable "attributes" {
  description = "List of nested attribute definitions"
  type = list(object({
    name = string
    type = string
  }))
  default = []
}

variable "global_secondary_indexes" {
  description = "Describe a GSI for the table"
  type = list(object({
    name            = string
    hash_key        = string
    range_key       = string
    projection_type = string
  }))
  default = []
}

variable "point_in_time_recovery" {
  description = "Whether to enable point-in-time recovery"
  type        = bool
  default     = true
}

variable "server_side_encryption" {
  description = "Whether to enable server-side encryption"
  type        = bool
  default     = true
}

variable "environment" {
  description = "Environment name"
  type        = string
}
```

### terraform/modules/dynamodb/main.tf
```hcl
resource "aws_dynamodb_table" "app_table" {
  name           = var.table_name
  billing_mode   = var.billing_mode
  hash_key       = var.hash_key
  range_key      = var.range_key

  dynamic "attribute" {
    for_each = var.attributes
    content {
      name = attribute.value.name
      type = attribute.value.type
    }
  }

  dynamic "global_secondary_index" {
    for_each = var.global_secondary_indexes
    content {
      name            = global_secondary_index.value.name
      hash_key        = global_secondary_index.value.hash_key
      range_key       = global_secondary_index.value.range_key
      projection_type = global_secondary_index.value.projection_type
    }
  }

  point_in_time_recovery {
    enabled = var.point_in_time_recovery
  }

  server_side_encryption {
    enabled = var.server_side_encryption
  }

  tags = {
    Environment = var.environment
    Name        = var.table_name
  }
}
```
### terraform/modules/s3/variables.tf
```hcl
variable "storage_bucket_name" {
  description = "Name of the S3 bucket for storage"
  type        = string
}

variable "environment" {
  description = "Environment tag"
  type        = string
}
```
### terraform/modules/s3/main.tf
```hcl
resource "aws_s3_bucket" "storage_bucket" {
  bucket = var.storage_bucket_name

  force_destroy = true

  tags = {
    Name        = var.storage_bucket_name
    Environment = var.environment
  }
}

resource "aws_s3_bucket_versioning" "storage_bucket_versioning" {
  bucket = aws_s3_bucket.storage_bucket.id

  versioning_configuration {
    status = "Suspended" # ãƒãƒ¼ã‚¸ãƒ§ãƒ‹ãƒ³ã‚°ç„¡åŠ¹åŒ–
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "storage_bucket_sse" {
  bucket = aws_s3_bucket.storage_bucket.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

```
### terraform/modules/iam/variables.tf
```hcl
variable "aws_account_id" {
  description = "AWS Account ID"
  type        = string
}

variable "aws_region" {
  description = "AWS region"
  type        = string
}

variable "s3_access_policy_name" {
  description = "Name of the S3 access IAM policy"
  type        = string
}

variable "dynamodb_access_policy_name" {
  description = "Name of the DynamoDB access IAM policy"
  type        = string
}

variable "web_s3_arn" {
  description = "ARN of the S3 web bucket"
  type        = string
}

variable "storage_s3_arn" {
  description = "ARN of the S3 storage bucket"
  type        = string
}

variable "ecs_task_execution_role_name" {
  description = "Name of the ECS task execution role"
  type        = string
}

variable "ecs_task_role_name" {
  description = "Name of the ECS task role"
  type        = string
}

variable "environment" {
  description = "Environment tag"
  type        = string
}

variable "dynamodb_table_arn" {
  description = "ARN of the DynamoDB table"
  type        = string
}
```

### terraform/modules/iam/main.tf
```hcl
# ECSã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ã«ã‚¢ã‚¿ãƒƒãƒã™ã‚‹S3ã‚¢ã‚¯ã‚»ã‚¹ç”¨ã®IAMãƒãƒªã‚·ãƒ¼ã®ä½œæˆ
resource "aws_iam_policy" "s3_access_policy" {
  name        = var.s3_access_policy_name
  description = "IAM policy for S3 access"
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect   = "Allow",
        Action   = ["s3:ListBucket"],
        Resource = var.storage_s3_arn
      },
      {
        Effect   = "Allow",
        Action   = ["s3:GetObject", "s3:PutObject", "s3:DeleteObject"],
        Resource = "${var.storage_s3_arn}/*"
      }
    ]
  })

  tags = {
    Name        = var.s3_access_policy_name
    Environment = var.environment
  }
}

# ECSã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ã«ã‚¢ã‚¿ãƒƒãƒã™ã‚‹DynamoDBã‚¢ã‚¯ã‚»ã‚¹ç”¨ã®IAMãƒãƒªã‚·ãƒ¼ã®ä½œæˆ
resource "aws_iam_policy" "dynamodb_access_policy" {
  name        = var.dynamodb_access_policy_name
  description = "IAM policy for DynamoDB access"
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect = "Allow",
        Action = [
          "dynamodb:Scan",
          "dynamodb:GetItem",
          "dynamodb:Query",
          "dynamodb:DescribeTable"
        ],
        Resource = var.dynamodb_table_arn
      }
    ]
  })

  tags = {
    Name        = var.dynamodb_access_policy_name
    Environment = var.environment
  }
}

resource "aws_iam_role" "ecs_task_execution_role" {
  name               = var.ecs_task_execution_role_name
  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Action    = "sts:AssumeRole",
        Effect    = "Allow",
        Principal = {
          Service = "ecs-tasks.amazonaws.com"
        }
      }
    ]
  })

  tags = {
    Name        = var.ecs_task_execution_role_name
    Environment = var.environment
  }
}

resource "aws_iam_role_policy_attachment" "ecs_task_execution_role_policy" {
  role       = aws_iam_role.ecs_task_execution_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

# ECSã‚¿ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ã®ä½œæˆ
resource "aws_iam_role" "ecs_task_role" {
  name               = var.ecs_task_role_name
  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Action    = "sts:AssumeRole",
        Effect    = "Allow",
        Principal = {
          Service = "ecs-tasks.amazonaws.com"
        }
      }
    ]
  })

  tags = {
    Name        = var.ecs_task_role_name
    Environment = var.environment
  }
}

# DynamoDBã‚¢ã‚¯ã‚»ã‚¹ç”¨ã®ãƒãƒªã‚·ãƒ¼ã®ã‚¢ã‚¿ãƒƒãƒãƒ¡ãƒ³ãƒˆ
resource "aws_iam_role_policy_attachment" "ecs_task_role_dynamodb_access" {
  role       = aws_iam_role.ecs_task_role.name
  policy_arn = aws_iam_policy.dynamodb_access_policy.arn
}

# S3ã‚¢ã‚¯ã‚»ã‚¹ç”¨ã®ãƒãƒªã‚·ãƒ¼ã®ã‚¢ã‚¿ãƒƒãƒãƒ¡ãƒ³ãƒˆ
resource "aws_iam_role_policy_attachment" "ecs_task_role_s3_access" {
  role       = aws_iam_role.ecs_task_role.name
  policy_arn = aws_iam_policy.s3_access_policy.arn
}
```